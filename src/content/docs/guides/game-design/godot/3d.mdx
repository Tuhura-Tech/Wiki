---
title: 3D Intro
description: This page introduces the concepts and practical aspects of making a 3D game in Godot 
sidebar:
    order: 2
---

This is a guide for making a 3-dimentional game in [Godot](https://godotengine.org/). If you are unfamiliar with Godot check out the [Godot basics](../basics) doc.

:::note[Version]
This guide is up-to-date with Godot 4.1.3 stable official release but will most likely work with any 4.x release.
:::

## Making the project

Making the project is just as easy as 2D.
![Godot new project window](../../../../../assets/godot/3D/NewProject.png)

Make sure your project is set to Forward+. 

:::note[Different renderers]
In 2D what renderer you choose has minimal effect on your final outcome due to how simple 2D images are to draw to a screen. With 3D there is a large disparity between the renderers. Read the notes next to each renderer before picking something other than Forward+.
:::

![Create root node, 3D Scene highlighted](../../../../../assets/godot/3D/CreateRootNode.png)

Set the root node to 3D and you're project is setup!  
Although go ahead and save the scene with Scene > Save Scene so you have the scene file saved.

Before adding things to your scene you should know how to move around in the viewport.  
Press down the middle mouse button to rotate the camera. Zoom in and out by scrolling. Shift+Middle Mouse to pan. Right click to turn and while right click is down you can press WASD-QE to fly. Press F to recentre the camera on the selected object.  
:::note[Devices without a middle mouse button]
If you do not have a middle mouse button to press, go to Editor > Editor Settings... > Editors > 3D and turn Emulate 3 Button Mouse on. Now you will use Alt to rotate and Shift to pan.
:::

## Making the player

### Basic player and platform

First, you're going to want to setup a platform for the player to stand on.

Simply add a StaticBody3D as a child of Node3D.
Then, add MeshInstance3D and CollisionShape3D as a child of StaticBody2D.
In the inspector on the right set MeshInstance3D's 'Mesh' property to New PlaneMesh.
Set the CollisionShape3D's 'Shape' property to New BoxShape3D.
Select CollisionShape3D and use the orange dots to shape the BoxShape3D to the same shape and size as the plane. 

Now, as a child of Node3D add CharacterBody3D.
Add as a child of CharacterBody3D a Camera3D and CollisionShape3D.
Set the collision shape to capsule.
Select the CharacterBody3D and move it up in the viewport until it's on top of the plane.
Move the camera up so it's where the 'head' should be on the character.
Give the CharacterBody3D a script and leave it as the default template script.
One final step, add a DirectionalLight3D as a child of Node3D and rotate it to point down, this can be anywhere in the scene.

Your scene should look something like this:
![Scene with player](../../../../../assets/godot/3D/InitialScene.png)
Now when you play the game and press **down** on the arrow keys you should move backwards off the platform and fall off, you should see the platform briefly as you fall.

### First person camera

Most modern 3D games have a first person camera, which can be pretty complicated. Open the script on your player.
In this script add these lines below `var gravity` and above `func _physics_process(delta):`:
```gdscript
# Get the player camera
@onready var main_camera := $Camera3D

# Make the camera variables
var camera_rotation = Vector2(0, 0)
var mouse_sensitivity := 0.005


func _ready() -> void:
	# Remove the mouse from the screen and just capture its movement
	Input.set_mouse_mode(Input.MOUSE_MODE_CAPTURED)


func _input(event) -> void:
	# If escape is pressed reveal the mouse
	if event.is_action_pressed("ui_cancel"):
		Input.set_mouse_mode(Input.MOUSE_MODE_VISIBLE)
	
	# Get the mouse movement
	if event is InputEventMouseMotion:
		# Get how much the mouse has moved and pass it onto the camera_look function
		var relative_position = event.relative * mouse_sensitivity
		camera_look(relative_position)

# Rotate the camera
func camera_look(movement: Vector2) -> void:
	# Add how much the camera has moved to the camera rotation
	camera_rotation += movement 
	# Stop the player from making the camera go upside down by looking too far up and down
	camera_rotation.y = clamp(camera_rotation.y, deg_to_rad(-90), deg_to_rad(90)) 
	
	# Reset the transform basis
	transform.basis = Basis()
	main_camera.transform.basis = Basis()
	
	# Finally rotate the object, the player and camera needs to rotate on the x and only the camera should rotate on the y
	rotate_object_local(Vector3.UP, -camera_rotation.x)
	main_camera.rotate_object_local(Vector3.RIGHT, -camera_rotation.y)
```
:::note[Understanding the code]
If you want to understand this code past reading the code, you should read the Godot docs on [Using Transforms](https://docs.godotengine.org/en/stable/tutorials/3d/using_transforms.html). Then try making a third-person camera! (A camera that hovers behind the shoulder of the player) You will need to add a capsule mesh to the CharacterBody3D so the player is visible. Keep in mind the y rotation shouldn't be centered on the camera but on the player's head.  
Here's an example of what it should look like:
![Third person camera](../../../../../assets/godot/3D/ThirdPersonCamera.gif)
:::
The mouse sensitivity variable controls how fast the camera moves, change it to suit your preference.

This is all you need for a basic character controller, but, as is good practice, you should change the input keys.

Go to Project > Project Settings... > Input Map and in `Add New Action` add 'forward', 'left', 'backward', 'right', and 'jump'.
Press the plus button next to each one and press the key you would like them to be.  
It is recommended 'forward' should be W, 'left' should be A, 'backward' should be S, 'right' should be D, and 'jump' should be spacebar.
In your script replace 'ui_up' with 'forward', 'ui_left' with 'left', 'ui_down' with 'backward', 'ui_right' with 'right', and 'ui_accept' with 'jump'.

'ui_cancel' does not need to be changed since it would be the key you press to exit anyway.

## Making the world

### More platforms

Now that you have a player you need more platforms to jump on.
Copy the StaticBody3D node and paste it as a child of Node3D.

Press R to change to scale mode and enlarge the StaticBody3D with the green square, and while we're at it use E to rotate the platform slightly and W to move it to the side so you have to jump to get to the platform.
You can go back to the select tool with Q.

Play the game and now you have another platform!

Although, if you're paying close attention you might notice the warning sign on the StaticBody3D.
To fix this reset the size of your StaticBody3D by either pressing CTRL+Z or selecting the reset button in the inspector on the scale which is found under Node3D > Transform.
Then select the MeshInstance3D and use scale mode to increase its size, and then select the CollisionShape3D with select mode and use the orange dots to increase its size to match the MeshInstance3D.

You might notice the collision shape is changing both collision shapes, for this new platform an the original one.
To fix that you'll need to select the CollisionShape3D and make a new BoxShape3D in the inspector since it's still using the same BoxShape3D as the previous platform.

### Interaction

You think pressing a button is easy.
You think it's just simple to add a thing that you walk up to and press a button to interact with when looking at it.

You were wrong, let's make a button that makes some text appear on the screen.

#### The text

First, the text. As a child of Camera3D add Label.
Give the label some text and move it to the center of the feint camera box.

It should look something like this:
![Label](../../../../../assets/godot/3D/Label.png)
Feel free to change the label to a RichTextLabel and mess around with making it look pretty.

After making your text go to CanvasItem > Visibility and turn Visible off, we will be making it visible with code.

#### The button

Add a new StaticBody3D, the only difference this time is make it a floating cube instead of a plane.

Select your new static body and go to the Node tab, which is found next to the inspector tab.
Go go groups and type in "Button" and press Add.

Add a RayCast3D as a child of your CharacterBody3D and move it to aim in the same direction as the camera and move it to the same position as where the camera originates.  
Your button and ray cast should look something like this:
![Button and ray cast](../../../../../assets/godot/3D/RayCastButton.png)
Then, decrease the ray cast's target position's y to -3 or lower.

Now, go to the script on your player and add an onready variable for the ray cast and the label.
When you have `@onready var name = ` you can just drag the node from the scene tree into the script and it will automatically fill in the variable path and add $.

Next add the action for your interact button, E or F are good buttons for basic world interaction, and name it something like 'interact'.  
Finally, somewhere in your `_physics_process(delta)` function, probably after the jumping code, add this code:
```gdscript
if Input.is_action_just_pressed("interact"):
	var object = ray_cast.get_collider()
	if object != null:
		if object.is_in_group("Button"):
			label.visible = true
```
